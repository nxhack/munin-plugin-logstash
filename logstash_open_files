#!/usr/bin/env perl

# Parameters supported:
#
# config
# autoconf
#
# Magic markers:
#%# family=auto
#%# capabilities=autoconf

use strict;
use warnings;
use LWP;
use JSON qw/decode_json/;

=head1 NAME

logstash_open_files - A munin plugin that collects the number of open files in your Logstash instances

=head1 APPLICABLE SYSTEMS

Logstash

=head1 CONFIGURATION

  [logstash_*]
    env.host 127.0.0.1
    env.port 9600

=head1 BUGS

None known so far. If you find any, let me know.

=head1 AUTHOR

Original Author:
Kentaro Yoshida - https://github.com/y-ken

Modified by Hirokazu MORIKAWA <morikw2@gmail.com>

=cut

my $host = exists $ENV{'host'} ? $ENV{'host'} : 'localhost';
my $port = exists $ENV{'port'} ? $ENV{'port'} : 9600;

my $ua = LWP::UserAgent->new;
$ua->timeout(10);

sub get_json_from_url {
    my $uri = shift;
    my $res = $ua->get($uri, 'Content-Type' => 'application/json' );
    Carp::confess($res->code . " for " . $uri) unless $res->is_success;
    my $data = do { local $@; eval { decode_json($res->content) } };
    die("Could not decode JSON from: " . $res->content) unless $data;
    return $data;
}

my $data = get_json_from_url("http://$host:$port/_node/stats/process");
#my %out = (max_file => 0, peak_open_file => 0, open_file => 0);
my %out = (peak_open_file => 0, open_file => 0);

#$out{max_file} = $data->{process}{max_file_descriptors};
$out{peak_open_file} = $data->{process}{peak_open_file_descriptors};
$out{open_file} = $data->{process}{open_file_descriptors};

if ($ARGV[0] and $ARGV[0] eq 'config') {
    print "graph_title Logstash (port $port) open files\n";
    print "graph_args --base 1000 -l 0\n";
    print "graph_vlabel number of open files\n";
    print "graph_category logstash\n";

    foreach my $name (keys %out) {
        print "$name.label $name\n"."$name.type GAUGE\n";
    }
}
elsif (!$ARGV[0] || $ARGV[0] eq 'autoconf') {
    foreach my $name (keys %out) {
        print "$name.value " . $out{$name} . "\n";
    }
}

exit(0);
