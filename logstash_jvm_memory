#!/usr/bin/env perl

# Parameters supported:
#
# config
# autoconf
#
# Magic markers:
#%# family=auto
#%# capabilities=autoconf

use strict;
use warnings;
use LWP;
use JSON qw/decode_json/;

=head1 NAME

logstash_jvm_memory - A munin plugin that collects JVM memory stats from the JVM of your Logstash instances

=head1 APPLICABLE SYSTEMS

Logstash

=head1 CONFIGURATION

  [logstash_*]
    env.host 127.0.0.1
    env.port 9600

=head1 BUGS

None known so far. If you find any, let me know.

=head1 AUTHOR

Original Author:
Tomas Doran (t0m) - c<< <bobtfish@bobtfish.net> >>
Kentaro Yoshida - https://github.com/y-ken

Modified by Hirokazu MORIKAWA <morikw2@gmail.com>

=cut

my $host = exists $ENV{'host'} ? $ENV{'host'} : 'localhost';
my $port = exists $ENV{'port'} ? $ENV{'port'} : 9600;

my $ua = LWP::UserAgent->new;
$ua->timeout(10);

sub get_json_from_url {
    my $uri = shift;
    my $res = $ua->get($uri, 'Content-Type' => 'application/json' );
    Carp::confess($res->code . " for " . $uri) unless $res->is_success;
    my $data = do { local $@; eval { decode_json($res->content) } };
    die("Could not decode JSON from: " . $res->content) unless $data;
    return $data;
}

my $data = get_json_from_url("http://$host:$port/_node/stats/jvm");
my %out = (heap_max => 0, heap_committed => 0, heap_used => 0, non_heap_committed => 0, non_heap_used => 0);

foreach my $name (grep { /_in_bytes$/ } keys %{ $data->{jvm}{mem} }) {
        my ($dname) = $name =~ m/(.+)_in_bytes$/;
        $out{$dname} += $data->{jvm}{mem}{$name};
}

if ($ARGV[0] and $ARGV[0] eq 'config') {
    print "graph_args --base 1024\n";
    print "graph_title Logstash (port $port) JVM memory usage\n";
    print "graph_category logstash\n";
    print "graph_vlabel Bytes\n";

    foreach my $name (keys %out) {
        print "$name.label $name\n"."$name.type GAUGE\n";
    }
}
elsif (!$ARGV[0] || $ARGV[0] eq 'autoconf') {
    foreach my $name (keys %out) {
        print "$name.value " . $out{$name} . "\n";
    }
}

exit(0);
