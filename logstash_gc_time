#!/usr/bin/env perl

# Parameters supported:
#
# config
# autoconf
#
# Magic markers:
#%# family=auto
#%# capabilities=autoconf

use strict;
use warnings;
use LWP;
use JSON qw/decode_json/;

=head1 NAME

logstash_gc_time - A munin plugin that collects garbage collection time stats of your Logstash instances

=head1 APPLICABLE SYSTEMS

Logstash

=head1 CONFIGURATION

  [logstash_*]
    env.host 127.0.0.1
    env.port 9600

=head1 BUGS

None known so far. If you find any, let me know.

=head1 AUTHOR

Original Author:
Timothy Messier (t0m) - c<< <tim.messier@gmail.com> >>
Kentaro Yoshida - https://github.com/y-ken

Modified by Hirokazu MORIKAWA <morikw2@gmail.com>

=cut

my $host = exists $ENV{'host'} ? $ENV{'host'} : 'localhost';
my $port = exists $ENV{'port'} ? $ENV{'port'} : 9600;

my $ua = LWP::UserAgent->new;
$ua->timeout(10);

sub get_json_from_url {
    my $uri = shift;
    my $res = $ua->get($uri, 'Content-Type' => 'application/json' );
    Carp::confess($res->code . " for " . $uri) unless $res->is_success;
    my $data = do { local $@; eval { decode_json($res->content) } };
    die("Could not decode JSON from: " . $res->content) unless $data;
    return $data;
}

my $data = get_json_from_url("http://$host:$port/_node/stats/jvm");
my %out = (young => 0, old => 0);

$out{young} = $data->{jvm}{gc}{collectors}{young}{collection_time_in_millis};
$out{old} = $data->{jvm}{gc}{collectors}{old}{collection_time_in_millis};

if ($ARGV[0] and $ARGV[0] eq 'config') {
    print "graph_title Logstash (port $port) GC time\n";
    print "graph_category logstash\n";
    print "graph_vlabel miliseconds\n";

    foreach my $name (keys %out) {
        print "$name.label $name\n"."$name.type COUNTER\n";
    }
}
elsif (!$ARGV[0] || $ARGV[0] eq 'autoconf') {
    foreach my $name (keys %out) {
        print "$name.value " . $out{$name} . "\n";
    }
}

exit(0);
